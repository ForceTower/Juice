/*
 * This file is part of the UNES Open Source Project.
 * UNES is licensed under the GNU GPLv3.
 *
 * Copyright (c) 2019. João Paulo Sena <joaopaulo761@gmail.com>
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <https://www.gnu.org/licenses/>.
 */
buildscript {
    repositories {
        jcenter()
    }
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.41'
    id "org.jmailen.kotlinter" version "2.1.0"
    id "maven-publish"
    id "signing"
}

repositories {
    mavenCentral()
    google()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
}

archivesBaseName = 'juice'
group = "com.github.forcetower"
version = version

task sourcesJar(type: Jar) {
    classifier 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier "javadoc"
    from javadoc.destinationDir
}

artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
}

signing {
    sign configurations.archives
}

publishing {
    publications {
        primary (MavenPublication) {
            groupId = 'com.github.forcetower'
            artifactId = 'juice'
            version = version

            pom {
                name = "Juice"
                description = "A SAGRES Scrapper Library"
                url = "http://github.com/ForceTower/Juice"
                from components.java

                licenses {
                    license {
                        name = "General Public License v3.0"
                        url = "https://www.gnu.org/licenses/gpl-3.0.html"
                    }
                }
                developers {
                    developer {
                        id = 'forcetower'
                        name = 'João Paulo Sena'
                        email = 'joaopaulo761@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/juice.git'
                    developerConnection = 'scm:git:ssh://github.com/juice.git'
                    url = 'http://www.forcetower.dev/juice'
                }
            }

            artifact(sourcesJar) {
                classifier = 'sources'
            }
            artifact(javadocJar) {
                classifier = 'javadoc'
            }

            pom.withXml {
                def pomFile = file("${project.buildDir}/generated-pom.xml")
                writeTo(pomFile)
                def pomAscFile = signing.sign(pomFile).signatureFiles[0]
                artifact(pomAscFile) {
                    classifier = null
                    extension = 'pom.asc'
                }
            }

            // create the signed artifacts
            project.tasks.signArchives.signatureFiles.each {
                artifact(it) {
                    def matcher = it.file =~ /-(sources|javadoc)\.jar\.asc$/
                    if (matcher.find()) {
                        classifier = matcher.group(1)
                    } else {
                        classifier = null
                    }
                    extension = 'jar.asc'
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "$buildDir/repos/releases"
            def snapshotsRepoUrl = "$buildDir/repos/snapshots"
            def snapshots = "https://oss.sonatype.org/content/repositories/snapshots"
            def staging = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            name "maven"
            credentials {
                username sonatypeUsername
                password sonatypePassword
            }}
    }
}

model {
    tasks.generatePomFileForPrimaryPublication {
        destination = file("$buildDir/generated-pom.xml")
    }
    tasks.publishPrimaryPublicationToMavenLocal {
        dependsOn project.tasks.signArchives
    }
    tasks.publishPrimaryPublicationToMavenRepository {
        dependsOn project.tasks.signArchives
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.3.41"
    implementation "org.jsoup:jsoup:1.11.2"
    implementation "com.squareup.okhttp3:okhttp:4.0.1"
    implementation "com.google.code.gson:gson:2.8.5"
    implementation "io.reactivex.rxjava2:rxkotlin:2.4.0-RC3"
    implementation "androidx.annotation:annotation:1.1.0"
    implementation "com.google.code.gson:gson:2.8.5"
    implementation 'org.json:json:20180130'
    implementation 'commons-codec:commons-codec:1.13'

    testImplementation "junit:junit:4.12"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.3.0-RC"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}